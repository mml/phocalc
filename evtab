#!/usr/bin/perl -w

require 5;

use strict;
use warnings;

use Getopt::Long;

Getopt::Long::Configure('bundling');

our $nd_filter = 0;
our $underexposure = 0;
our $overexposure = 0;

GetOptions(
  'nd_filter=f' => \$nd_filter,
  'underexposure=f' => \$underexposure,
  'overexposure=f' => \$overexposure,
);

if ($overexposure != 0 and $underexposure != 0) {
  die "Can't specify both under- and over-exposure together.";
}

our $stop0 = 0;
our @stops = qw(
  1.0  1.1  1.3
  1.4  1.6  1.8
  2    2.2  2.5
  2.8  3.2  3.5
  4    4.5  5
  5.6  6.3  7.1
  8    9    10
  11   13   14
  16   18   20
  22   25   28
  32   36   40
);

our $ss0 = 5;
our @ss = qw(
  30" 25" 20"
  15" 12" 10"
  8" 6.4" 5"
  4" 3.2" 2.5"
  2" 1.6" 1.3"
  1 1.3 1.6
  2 2.5 3.2
  4 5 6.4
  8 10 12
  15 20 25
  30 40 50
  60 80 100
  125 160 200
  250 320 400
  500 640 800
  1000 1250 1600
  2000 2500 3200
);

sub round3($) {
  return (sprintf "%.f", 3*$_[0])/3;
}

sub shutter3 {
  my($shutter_3e) = @_;
  my $index = 3*$ss0-$shutter_3e;
  if ($index >= 0 and $index <= $#ss) {
    return $ss[$index];
  }
  return "-";
}

my($lv, $iso) = @ARGV;

my $iso_e = round3((log $iso)/(log 2) - (log 100)/(log 2));

# Convert log 10 ND filter numbers into 1/3rd stops if they look like a decimal value.
my $nd_filter_e = $nd_filter;
if ($nd_filter =~ /\./) {
  $nd_filter_e = (sprintf "%.f", 3*log(10**$nd_filter)/log(2))/3;
}

my $ev = $lv + $iso_e - $nd_filter_e + $overexposure - $underexposure;

printf "Conditions: %4.1f\n", $lv;
printf "Film speed: %4.1f\n", $iso_e;
printf "ND Filter:  %4.1f\n", -$nd_filter_e;
if ($overexposure != 0) {
  printf "EC        : %4.1f stops over\n", $overexposure;
}
if ($underexposure != 0) {
  printf "EC        : %4.1f stops under\n", $underexposure;
}
printf "Exposure v: %4.1f\n", $ev;

# This loop is denominated in third-stops for integer math
for (my $stop = 0; $stop <= $#stops; ++$stop) {
  my $aperture_3e = 3 * $stop0 - $stop;
  my $shutter_3e = -3 * $ev - $aperture_3e;

  printf "f/%-3.3s  %s\n", $stops[$stop], shutter3($shutter_3e);
}

# f/16 ... 1/16 = sqrt(2)**-8
# f/11 ... 1/11.3 = sqrt(2)**-7

# 1/125 =~ 2 ** -7
# 1/250 =~ 2 ** -8

__DATA__
LV TYPE OF LIGHTING SITUATION
-6 Night, away from city lights, subject under starlight only.
-5 Night, away from city lights, subject under crescent moon.
-4 Night, away from city lights, subject under half moon. Meteors (during showers, with time exposure).
-3 Night, away from city lights, subject under full moon.
-2 Night, away from city lights, snowscape under full moon.
-1 Subjects lit by dim ambient artificial light.
0 Subjects lit by dim ambient artificial light.
1 Distant view of lighted skyline.
2 Lightning (with time exposure). Total eclipse of moon.
3 Fireworks (with time exposure).
4 Candle lit close-ups. Christmas lights, floodlit buildings, fountains, and monuments. Subjects under bright street lamps.
5 Night home interiors, average light. School or church auditoriums. Subjects lit by campfires or bonfires.
6 Brightly lit home interiors at night. Fairs, amusement parks.
7 Bottom of rainforest canopy. Brightly lighted nighttime streets. Indoor sports. Stage shows, circuses.
8 Las Vegas or Times Square at night. Store windows. Campfires, bonfires, burning buildings. Ice shows, football, baseball etc. at night. Interiors with bright florescent lights.
9 Landscapes, city skylines 10 minutes after sunset. Neon lights, spotlighted subjects.
10 Landscapes and skylines immediately after sunset. Crescent moon (long lens).
11 Sunsets. Subjects in deep shade.
12 Half moon (long lens). Subject in open shade or heavy overcast.
13 Gibbous moon (long lens). Subjects in cloudy-bright light (no shadows).
14 Full moon (long lens). Subjects in weak, hazy sun.
15 Subjects in bright or hazy sun (Sunny f/16 rule).
16 Subjects in bright daylight on sand or snow.
17 Rarely encountered in nature. Some man made lighting.
18 Rarely encountered in nature. Some man made lighting.
19 Rarely encountered in nature. Some man made lighting.
20 Rarely encountered in nature. Some man made lighting.
21 Rarely encountered in nature. Some man made lighting.
22 Extremely bright. Rarely encountered in nature.
23 Extremely bright. Rarely encountered in nature.
