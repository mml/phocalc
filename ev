#!/usr/bin/perl -w
use strict;
use warnings;

our $r2 = sqrt 2;

if ($#ARGV < 2) {
  die "USAGE: ev APERTURE SHUTTER ISO\n";
}

my ($f, $shutter, $iso) = @ARGV;

my %description;

while (<DATA>) {
  my ($k, $v) = split ' ', $_, 2;
  $description{$k} = $v;
}

# We're counting 1/3 stops.

my $f_e = sprintf '%.f', 3 * (log 1/$f)/(log $r2);
my $shutter_e;
my $shutter_s;
if ($shutter =~ /^(\d+)'$/) {
  $shutter_e = sprintf '%.f', 3 * (log $1)/(log 2);
  $shutter_s = $1;
} else {
  $shutter_e = sprintf '%.f', 3 * (log 1/$shutter)/(log 2);
  $shutter_s = "1/$shutter";
}

# We want ISO 100 to be 5, but it comes out as 6 2/3.
my $iso_e = sprintf '%.f', (3 * (log $iso)/(log 2))-5;

binmode STDOUT, ':utf8';

print "f/$f ", vulgar($f_e), "\n";
print "$shutter_s s ", vulgar($shutter_e), "\n";
print "ASA $iso ", vulgar($iso_e), "\n";

my $total = $f_e + $shutter_e + $iso_e;
print "TOTAL (in thirds) $total\n";

# -30 is EV 15, Sunny 16
my $ev3 = -(($total + 30) - 15*3);

print "EV ", vulgar($ev3), "\n";
print "$description{sprintf '%.f', ($ev3/3)} \n";

sub vulgar {
  my ($x) = @_;
  my $r = $x % 3;
  my $v = '   ';
  if ($r == 1) {
    # $v = "\x{2153}";
    $v = '1/3';
  } elsif ($r == 2) {
    # $v = "\x{2154}";
    $v = '2/3';
  }
  return sprintf "%d %s", int($x/3), $v;
}

__DATA__
EV TYPE OF LIGHTING SITUATION
-6 Night, away from city lights, subject under starlight only.
-5 Night, away from city lights, subject under crescent moon.
-4 Night, away from city lights, subject under half moon. Meteors (during showers, with time exposure).
-3 Night, away from city lights, subject under full moon.
-2 Night, away from city lights, snowscape under full moon.
-1 Subjects lit by dim ambient artificial light.
0 Subjects lit by dim ambient artificial light.
1 Distant view of lighted skyline.
2 Lightning (with time exposure). Total eclipse of moon.
3 Fireworks (with time exposure).
4 Candle lit close-ups. Christmas lights, floodlit buildings, fountains, and monuments. Subjects under bright street lamps.
5 Night home interiors, average light. School or church auditoriums. Subjects lit by campfires or bonfires.
6 Brightly lit home interiors at night. Fairs, amusement parks.
7 Bottom of rainforest canopy. Brightly lighted nighttime streets. Indoor sports. Stage shows, circuses.
8 Las Vegas or Times Square at night. Store windows. Campfires, bonfires, burning buildings. Ice shows, football, baseball etc. at night. Interiors with bright florescent lights.
9 Landscapes, city skylines 10 minutes after sunset. Neon lights, spotlighted subjects.
10 Landscapes and skylines immediately after sunset. Crescent moon (long lens).
11 Sunsets. Subjects in deep shade.
12 Half moon (long lens). Subject in open shade or heavy overcast.
13 Gibbous moon (long lens). Subjects in cloudy-bright light (no shadows).
14 Full moon (long lens). Subjects in weak, hazy sun.
15 Subjects in bright or hazy sun (Sunny f/16 rule).
16 Subjects in bright daylight on sand or snow.
17 Rarely encountered in nature. Some man made lighting.
18 Rarely encountered in nature. Some man made lighting.
19 Rarely encountered in nature. Some man made lighting.
20 Rarely encountered in nature. Some man made lighting.
21 Rarely encountered in nature. Some man made lighting.
22 Extremely bright. Rarely encountered in nature.
23 Extremely bright. Rarely encountered in nature.
